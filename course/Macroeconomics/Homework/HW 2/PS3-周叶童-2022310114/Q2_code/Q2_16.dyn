% Q2_16

%----------------------------------------------------------------
% 0. Housekeeping
%----------------------------------------------------------------
close all;

%----------------------------------------------------------------
% 1. Defining variables
%----------------------------------------------------------------
var C L W R I K A Y; 
varexo ea; 

parameters alf bta gma dlt rho sig;

%----------------------------------------------------------------
% 2. Calibration
%----------------------------------------------------------------
alf = 0.4;  bta = 0.98;  gma=0.4;
dlt = 0.05; rho = 0.9;    sig = 0.01;   

%----------------------------------------------------------------
% 3. Model
%----------------------------------------------------------------
model; 
  C/(1-L)=(gma/(1-gma))*W;
  1=bta*(C/C(+1))*(R(+1)+1-dlt);
  W=(1-alf)*A*K(-1)^(alf)*L^(-alf);
  R=alf*A*K(-1)^(alf-1)*L^(1-alf);
  Y=C+I;
  K=(1-dlt)*K(-1)+I;
  Y=A*K(-1)^(alf)*L^(1-alf);
  log(A)=rho*log(A(-1))+ea;
end;

%----------------------------------------------------------------
% 4. Computation
%----------------------------------------------------------------
 initval; % not necessary if steadystate file specified
  C = 1; K = 6;  
   L = 0.5;  A = 1; 
 Y = C+dlt*K ; 
I=0.3 ; W=2;
R=0.07;
 end;

 steady;

shocks;
  var  ea = sig^2;
end;

stoch_simul(order=1,irf=50) C L W R I K A Y;
